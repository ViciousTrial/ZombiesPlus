plugins {
    id 'com.github.johnrengelman.shadow'
}

def generatedResources = file("src/generated/resources")

loom {
    forge {
        mixinConfig "template_mod.modMixins.json"
    }

    runs {
        data {
            data()
            programArgs "--all", "--mod", mod_id
            programArgs "--output", generatedResources.absolutePath
        }
    }
}

architectury {
    platformSetupLoomIde()
    forge()
}

configurations {
    common {
        canBeResolved = true
        canBeConsumed = false
    }
    compileClasspath.extendsFrom common
    runtimeClasspath.extendsFrom common
    developmentForge.extendsFrom common

    // Files in this configuration will be bundled into your mod using the Shadow plugin.
    // Don't use the `shadow` configuration from the plugin itself as it's meant for excluding files.
    shadowBundle {
        canBeResolved = true
        canBeConsumed = false
    }
}

dependencies {
    forge "net.minecraftforge:forge:${minecraft_version}-${forge_version}"

    modImplementation "dev.architectury:architectury-forge:${architectury_api_version}"

    common(project(path: ':common', configuration: 'namedElements')) { transitive false }
    shadowBundle project(path: ':common', configuration: 'transformProductionForge')
}


project.ext.set("mod_version", rootProject.ext.modVersion)

version = project.mod_version
group = project.mod_group_id

processResources {
    inputs.property 'version', project.version

    filesMatching('META-INF/mods.toml') {
        expand(
            minecraft_version: minecraft_version,
            minecraft_version_range: minecraft_version_range,
            forge_version: forge_version,
            forge_version_range: forge_version_range,
            loader_version_range: loader_version_range,
            mod_id: mod_id,
            mod_name: mod_name,
            mod_license: mod_license,
            mod_version: mod_version,
            mod_authors: mod_authors,
            mod_description: mod_description,
            mod_version_number: mod_version_number,
            architectury_api_version: architectury_api_version
        )
    }
}

/* sourceSets {
    main {
        resources.srcDir file("src/main/resources")
    }
} */

shadowJar {
    configurations = [project.configurations.shadowBundle]
    archiveClassifier = 'dev-shadow'
}

remapJar {
    input.set shadowJar.archiveFile
}
